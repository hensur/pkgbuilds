# Maintainer: Niklas <dev@n1klas.net>

pkgname='homegear-management'
pkgdesc='Homegear Management'
pkgver=0.8.0
pkgrel=1
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
license=('GPL3')
url="https://homegear.eu"
depends=('libhomegear-base' 'libhomegear-node')
source=("git://github.com/Homegear/${pkgname}.git")
sha256sums=('SKIP')

prepare() {
    cd "${srcdir}/${pkgname}"

    # Use the our version number instead of the script which would have fetched it from Github
    sed -i -e "s#m4_esyscmd_s(\[./getVersion.sh\])#${pkgver}#" configure.ac

    sed -i -e "/#include <sys\/sysctl.h>/d" src/main.cpp
}

build() {
    cd "${srcdir}/${pkgname}"

    ./bootstrap
    ./configure --prefix=/usr --localstatedir=/var --sysconfdir=/etc --libdir=/usr/lib
    
    make
}

package() {
    cd "${srcdir}/${pkgname}"

    make DESTDIR="${pkgdir}" install
}
